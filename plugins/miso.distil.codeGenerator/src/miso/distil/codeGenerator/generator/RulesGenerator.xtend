/*
 * generated by Xtext
 */
package miso.distil.codeGenerator.generator

import codeGeneratorModel.Entity
import codeGeneratorModel.Artifact
import codeGeneratorModel.Root
import codeGeneratorModel.ServiceEnum
import com.google.inject.Inject

import miso.distil.codeGenerator.generator.entities.generateMain
import miso.distil.codeGenerator.generator.entities.generateArtifactClass
import miso.distil.codeGenerator.generator.entities.generateEntityClass

import miso.distil.codeGenerator.generator.basic.generateBasicCodes
import miso.distil.codeGenerator.generator.basic.generateBasicParam
import miso.distil.codeGenerator.generator.basic.generateBasicSpark

import miso.distil.codeGenerator.generator.custom.generateCustomDB
import miso.distil.codeGenerator.generator.custom.generateJson
import miso.distil.codeGenerator.generator.custom.generateCustomHtml

import miso.distil.codeGenerator.generator.html.generateHtmlJson
import miso.distil.codeGenerator.generator.html.generateHtmlSpark
import miso.distil.codeGenerator.generator.html.generateHtmlLinks
import miso.distil.codeGenerator.generator.html.generateHtmlView

import miso.distil.codeGenerator.generator.services.generateMultiService
import miso.distil.codeGenerator.generator.services.generateSimpleService
import miso.distil.codeGenerator.generator.services.generateServicesSpark

import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.IFileSystemAccess
import org.eclipse.xtext.generator.IGenerator
import org.eclipse.emf.common.util.EList
import codeGeneratorModel.SimpleService
import codeGeneratorModel.MultiService
import codeGeneratorModel.Service

/**
 * This class contains custom generation rules. 
 * 
 * @author Carlos Carrascal
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#code-generation
 */
class RulesGenerator implements IGenerator {
	
	@Inject packages pack
	
	@Inject generateMain genMain
	@Inject generateCustomDB genCusDB
	@Inject generateJson genJso
	@Inject generateArtifactClass genArtifact
	@Inject generateEntityClass genEnti
	
	@Inject generateBasicCodes genBasCod
	@Inject generateBasicSpark genBasSpa
	@Inject generateBasicParam genBasPar
	
	@Inject generateHtmlSpark genHtmSpa
	@Inject generateHtmlJson genHtmJso
	@Inject generateHtmlLinks genHtmLin
	@Inject generateHtmlView genHtmVie
	
	@Inject generateMultiService genMulSer
	@Inject generateSimpleService genSimSer
	@Inject generateServicesSpark genSerSpa
	
	@Inject generateCustomHtml genCusHtm
		
	override void doGenerate(Resource resource, IFileSystemAccess fsa) {
		
		// Root file (overwrite existing files)
		fsa.generateFile(
			pack.RootStri + "/Main.java",
			genMain.write())
			
		// Custom DB (not overwrite existing files)
		fsa.generateFile(
			pack.RootStri + "/CustomDB.java",
			GeneratorOutputConfiguration::GEN_ONCE_OUTPUT,
			genCusDB.write())
			    	
		// Auxiliar entities (overwrite)
		resource.allContents.filter(Entity).forEach[
			fsa.generateFile(
				pack.EntitiesStr + "/" + it.name + ".java",
		    	genEnti.write(it))
		]
		
		// Simple services (once)
		resource.allContents.filter(SimpleService).forEach[
			fsa.generateFile(
				pack.ServicesStr + "/" + "Service" + it.name + ".java",
				GeneratorOutputConfiguration::GEN_ONCE_OUTPUT,
		    	genSimSer.write(it))
		]
		
		// Multi services (overwrite)
		resource.allContents.filter(MultiService).forEach[
			fsa.generateFile(
				pack.ServicesStr + "/" + "Service" + it.name + ".java",
		    	genMulSer.write(it))
		]
		
		// Spark services (overwrite)
		if(!resource.allContents.filter(Root).last.services.empty) {
			fsa.generateFile(
					pack.ServicesStr + "/" + "ServicesSpark.java",
			    	genSerSpa.write(resource.allContents.filter(Root).last.services as EList<Service>))
		}

		// For each Artifact
		for(artifact : resource.allContents.filter(Root).last.artifacts as EList<Artifact>) {
			var packageArtifact = pack.getArtifactStri(artifact) + "/"
			var packageBasic = pack.getBasicStri(artifact) + "/Basic" + artifact.name.toFirstUpper
			var packageHtml = pack.getHtmlStri(artifact) + "/Html" + artifact.name.toFirstUpper
			
			// Artifact file (overwrite)
			fsa.generateFile(
			    packageArtifact + artifact.name + ".java",
			    genArtifact.write(artifact))

			// Update/Upload methods (once)
			if(!artifact.basicServices.empty) {
				fsa.generateFile(
					packageArtifact + artifact.name + "Json.java",
					GeneratorOutputConfiguration::GEN_ONCE_OUTPUT,
					genJso.write(artifact))
			}
			
			// Html custom cover (once)
			if(artifact.basicServices.contains(ServiceEnum.READ) || artifact.basicServices.contains(ServiceEnum.READ_ALL) || 
				artifact.basicServices.contains(ServiceEnum.UPDATE) || artifact.basicServices.contains(ServiceEnum.UPLOAD) ||
				artifact.basicServices.contains(ServiceEnum.SEARCH))
			{
				fsa.generateFile(
					packageArtifact + "Custom" + artifact.name + "Html.java",
					GeneratorOutputConfiguration::GEN_ONCE_OUTPUT,
					genCusHtm.write(artifact))
			}
							
			// Basic files (overwrite)
			if(!artifact.basicServices.empty)
			{
				fsa.generateFile(
					packageBasic + "Spark.java",
					genBasSpa.write(artifact))
					
				fsa.generateFile(
					packageBasic + "Codes.java",
					genBasCod.write(artifact))
					
				fsa.generateFile(
					packageBasic + "Param.java",
					genBasPar.write(artifact))
			}

			// Html Cover (overwrite)
			if(artifact.basicServices.contains(ServiceEnum.READ) || artifact.basicServices.contains(ServiceEnum.READ_ALL) || 
				artifact.basicServices.contains(ServiceEnum.UPDATE) || artifact.basicServices.contains(ServiceEnum.UPLOAD) ||
				artifact.basicServices.contains(ServiceEnum.SEARCH))
			{				
				fsa.generateFile(
					packageHtml + "Spark.java",
					genHtmSpa.write(artifact))
					
				fsa.generateFile(
					packageHtml + "View.java",
					genHtmVie.write(artifact))
					
				fsa.generateFile(
					packageHtml + "Json.java",
					genHtmJso.write(artifact))
			}
			
			// Lings (overwrite)
			if(artifact.basicServices.contains(ServiceEnum.READ) || artifact.basicServices.contains(ServiceEnum.DOWNLOAD) || 
				artifact.basicServices.contains(ServiceEnum.UPDATE) || artifact.basicServices.contains(ServiceEnum.UPLOAD))
			{
				fsa.generateFile(
					packageHtml + "Links.java",
					genHtmLin.write(artifact))
			}			
		}
	}
}
